// Step Event for obj_player
if (keyboard_check_pressed(ord("E"))) {
    show_debug_message("E key pressed");

    if (global.current_skin == "player") {
        show_debug_message("Switching to icetruck");
        obj_controller.switch_skin("icetruck");
    } else if (global.current_skin == "icetruck") {
        show_debug_message("Switching to player");
        obj_controller.switch_skin("player");
    }
}

// Handle movement
var right_key = keyboard_check(ord("D")) || keyboard_check(vk_right);
var left_key = keyboard_check(ord("A")) || keyboard_check(vk_left);
var up_key = keyboard_check(ord("W")) || keyboard_check(vk_up);
var down_key = keyboard_check(ord("S")) || keyboard_check(vk_down);

// Get xspd & yspd
xspd = (right_key - left_key) * move_spd;
yspd = (down_key - up_key) * move_spd;

// Pause
if instance_exists(obj_pauser) {
    xspd = 0;
    yspd = 0;
}

// Set sprite
mask_index = sprite[DOWN];
if (yspd < 0) {
    if (xspd < 0) { face = UP_LEFT; }
    else if (xspd > 0) { face = UP_RIGHT; }
    else { face = UP; }
}
else if (yspd > 0) {
    if (xspd < 0) { face = DOWN_LEFT; }
    else if (xspd > 0) { face = DOWN_RIGHT; }
    else { face = DOWN; }
}
else {
    if (xspd > 0) { face = RIGHT; }
    else if (xspd < 0) { face = LEFT; }
}

sprite_index = sprite[face];

// Collisions
if (place_meeting(x + xspd, y, obj_wall) || (driving == false && place_meeting(x + xspd, y, obj_icetruck))) {
    xspd = 0;
}
if (place_meeting(x, y + yspd, obj_wall) || (driving == false && place_meeting(x, y + yspd, obj_icetruck))) {
    yspd = 0;
}

// Move the player
x += xspd;
y += yspd;

// Animate
if (xspd == 0 && yspd == 0) {
    image_index = 0;
}

// Depth
depth = -bbox_bottom;