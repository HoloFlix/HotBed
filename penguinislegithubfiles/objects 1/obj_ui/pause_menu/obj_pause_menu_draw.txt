// obj_pause_menu_draw.txt
draw_set_font(fnt_bumbastika_sml);

// Dynamically get width and height of menu
var _new_w = 0;
for (var i = 0; i < op_length; i++) {
    var _op_w = string_width(option[menu_level, i]);
    _new_w = max(_new_w, _op_w);
}
width = _new_w + op_border * 2;
height = op_border * 2 + string_height(option[0, 0]) * op_length;

// Center menu
x = camera_get_view_x(view_camera[0]) + (camera_get_view_width(view_camera[0]) - width) / 2;
y = camera_get_view_y(view_camera[0]) + (camera_get_view_height(view_camera[0]) - height) / 2;

// Draw the menu background
draw_sprite_ext(spr_pause_menu, 0, x, y, width / sprite_get_width(spr_pause_menu), height / sprite_get_height(spr_pause_menu), 0, c_white, 1);

// Draw the options
draw_set_valign(fa_top);
draw_set_halign(fa_center);
for (var i = 0; i < op_length; i++) {
    var _c = c_white;
    if (pos == i) { _c = c_yellow; }
    draw_text_color(x + width / 2, y + op_border + op_space * i, option[menu_level, i], _c, _c, _c, _c, 1); // Centered text
}
